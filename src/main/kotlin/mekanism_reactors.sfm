Name "Mekanism Reactors"

Every tick do
  Input fe:: from Interface, FluxAccessor
  Output fe:: to PowerMachine top side
  Output fe:: to PowerMachineBottom bottom side
End

-- Yellow Cake
Every 20 ticks do
  Input uranium_ingot from Interface
  Output to YellowCake top side
Forget
  Input from YellowCake bottom side
  Output to Controller
End

-- H2/O2
Every redstone pulse do
  Input fluid::water from WaterEntanglo bottom side
  Output fluid:: to WaterInput, H2O2 top side
Forget
  Input chemical:: from H2O2 top side
  Output chemical:: to H2Entanglo top side
Forget
  Input chemical:: from H2O2 bottom side
  Output chemical:: to O2Entanglo top side
End

-- Fissile Fuel
Every 20 ticks do
  Input item:: from Interface, Controller
  Output 16 sulfur to each SulfurInput top side
  Output 16 yellow_cake_uranium to each YellowCakeInput top side
  Output 16 fluorite_block to each FluoriteBlockInput top side
Forget
  Input chemical:: from O2Entanglo bottom side
  Output chemical:: to O2Input top side
End

-- Pellet
Every 20+5 ticks do
  Input fluid::water from WaterEntanglo bottom side
  Input fluorite_dust from Interface
  Output fluid::, item:: to PelletChamber top side
Forget
  Input item:: from PelletChamber bottom side
  Output to Interface
End

-- Deuterium
Every redstone pulse do
  Input chemical:: from Deuterium bottom side
  Output 16000 chemical:: to DeuteriumEntanglo top side
Forget
  Input chemical:: from Deuterium top side
  Output 16000 chemical:: to O2Entanglo top side
End