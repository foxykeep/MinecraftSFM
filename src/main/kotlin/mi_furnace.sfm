Name "MI Furnace"

-- Power
Every tick do
  Input fe:: from PowerEntanglo bottom side
  Output fe:: to each PowerMachine
End

-- Fluids
Every 20 ticks do
  Input fluid::steam from SteamEntanglo bottom side
  Output fluid:: to FluidInputHatch
End

-- Coke Oven
Every 20 ticks do
  Input minecraft:coal from ControllerInput
  Output to ItemInputHatch
Forget
  Input coke from ItemOutputHatch
  Output to ControllerMI
Forget
  Input fluid:: from FluidOutputHatch
  Output fluid:: to LubricantMixer
Forget
  Input fluid:: from LubricantMixer
  Output fluid:: to ControllerMI
Forget
  Input redstone from ControllerInput
  Output retain 64 to LubricantMixer
End

-- Rubber Sheet
Every 20 ticks do
  Input fluid::raw_synthetic_oil from ControllerMI
  Output fluid:: to BlastOilInputHatch
Forget
  Input sulfur from ControllerInput
  Output retain 16 to each SyntheticRubberMixer
Forget
  Input fluid::synthetic_oil from BlastFluidOutputHatch
  Output fluid:: to each SyntheticRubberMixer
Forget
  Input fluid::synthetic_rubber from SyntheticRubberMixer
  Output fluid:: to each RubberSheetMixer
Forget
  Input paper from ControllerInput
  Output retain 64 to each RubberSheetMixer
Forget
  Input rubber_sheet from RubberSheetMixer
  Output to ControllerMI
End

-- Electric Blast Furnace - Stainless Steel
Every 20 ticks do
  Input stainless_steel_dust from ControllerMI
  Output to StainlessSteelFurnaceInput
Forget
  Input from StainlessSteelFurnaceOutput
  Output to HotIngotBarrel
Forget
  Input stainless_steel_hot_ingot from HotIngotBarrel
  Output to StainlessSteelFreezerInput
Forget
  Input from StainlessSteelFreezerOutput
  Output to ControllerMI
End

-- Electric Blast Furnace - Kanthal
Every 20 ticks do
  Input kanthal_dust from ControllerMI
  Output to KanthalFurnaceInput
Forget
  Input from KanthalFurnaceOutput
  Output to HotIngotBarrel
Forget
  Input kanthal_hot_ingot from HotIngotBarrel
  Output to KanthalFreezerInput
Forget
  Input from KanthalFreezerOutput
  Output to ControllerMI
End

-- Electric Blast Furnace - Annealed Copper
Every 20 ticks do
  Input copper_dust, fluid::oxygen from ControllerInput, ControllerMI
  Output item:: to AnnealedCopperFurnaceItemInput
  Output fluid:: to AnnealedCopperFurnaceFluidInput
Forget
  Input from AnnealedCopperFurnaceOutput
  Output to HotIngotBarrel
Forget
  Input annealed_copper_hot_ingot from HotIngotBarrel
  Output to AnnealedCopperFreezerInput
Forget
  Input from AnnealedCopperFreezerOutput
  Output to ControllerMI
End